import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'java-library'
    id 'c'
}

group = 'de.rasmusantons'
version = '0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

model {
    platforms {
        x64 {
            architecture "x86_64"
        }
    }

    components {
        cubiomes(NativeLibrarySpec) {
            targetPlatform "x64"
            sources {
                c {
                    source {
                        srcDirs "src/cubiomes/c"
                        include "*.c"
                    }
                }
            }
            binaries.all {
                cCompiler.args '-O3'
                cCompiler.args '-fPIC'
            }
        }

        cubij(NativeLibrarySpec) {
            targetPlatform "x64"
            sources {
                c {
                    source {
                        lib project: ':', library: 'cubiomes', linkage: 'static'
                    }
                }
            }
            binaries.all {
                def jvmHome = Jvm.current().javaHome
                if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/linux"
                    cCompiler.args '-I', "${projectDir}/src/cubiomes/c"
                    cCompiler.args '-O3'
                    cCompiler.args '-fPIC'
                }
            }
        }
    }
}

tasks.register('buildNativeLib', Copy) {
    group = "build"
    dependsOn 'cubijSharedLibrary'

    from layout.buildDirectory.dir('libs/cubij/shared').get()
    include 'libcubij.so'
    into layout.projectDirectory.dir('src/main/resources')
}

tasks.register('genCHeaders', Exec) {
    group = "build"
    commandLine 'javac', '-h', 'src/cubij/c',
            'src/main/java/de/rasmusantons/cubiomes/Generator.java',
            'src/main/java/de/rasmusantons/cubiomes/MCVersion.java',
            'src/main/java/de/rasmusantons/cubiomes/BiomeID.java',
            'src/main/java/de/rasmusantons/cubiomes/Dimension.java',
            'src/main/java/de/rasmusantons/cubiomes/Range.java'
}

clean.doFirst {
    delete layout.projectDirectory.file('src/main/resources/libcubij.so')
}

processResources.dependsOn 'buildNativeLib'
classes.dependsOn 'buildNativeLib'

test {
    useJUnitPlatform()
}
