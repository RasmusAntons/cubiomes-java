import org.gradle.internal.jvm.Jvm
import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'java-library'
    id 'c'
}

group = 'de.rasmusantons'
version = '0.0.6'

def isWindows = Os.isFamily(Os.FAMILY_WINDOWS)
def crossGccWindows = 'x86_64-w64-mingw32-gcc'
def canCrossCompileWindows = file("/usr/bin/${crossGccWindows}").exists()
def crossGccAarch64 = 'aarch64-linux-gnu-gcc'
def canCrossCompileAarch64 = file("/usr/bin/${crossGccAarch64}").exists()

compileJava {
    sourceCompatibility = 17
    targetCompatibility = 17
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.11.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

static File appendBinarySuffix(File binaryFile, String suffix) {
    int extensionSeparatorIndex = binaryFile.path.lastIndexOf('.')
    return new File(binaryFile.path.substring(0, extensionSeparatorIndex) + suffix + binaryFile.path.substring(extensionSeparatorIndex))
}

model {
    platforms {
        linux_x64 {
            operatingSystem 'linux'
            architecture "x86_64"
        }
        linux_aarch64 {
            operatingSystem 'linux'
            architecture "aarch64"
        }
        windows_x64 {
            operatingSystem 'windows'
            architecture "x86_64"
        }
    }

    toolChains {
        if (!isWindows && canCrossCompileWindows) {
            gcc_aarch64(Gcc) {
                target("linux_aarch64") {
                    cCompiler.executable crossGccAarch64
                    linker.executable crossGccAarch64
                }
            }
            mingw(Gcc) {
                target("windows_x64") {
                    cCompiler.executable crossGccWindows
                    linker.executable crossGccWindows
                }
            }
        }
    }

    components {
        cubiomes(NativeLibrarySpec) {
            if (!isWindows) {
                targetPlatform "linux_x64"
            }
            if (!isWindows && canCrossCompileAarch64) {
                targetPlatform "linux_aarch64"
            }
            if (isWindows || canCrossCompileWindows) {
                targetPlatform "windows_x64"
            }
            sources {
                c {
                    source {
                        srcDirs "src/cubiomes/c"
                        include "*.c"
                    }
                }
            }
            binaries.all {
                cCompiler.args '-O3'
                if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-fPIC'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args '-D_WIN32'
                }
            }
        }

        cubij(NativeLibrarySpec) {
            if (!isWindows) {
                targetPlatform "linux_x64"
            }
            if (!isWindows && canCrossCompileAarch64) {
                targetPlatform "linux_aarch64"
            }
            if (isWindows || canCrossCompileWindows) {
                targetPlatform "windows_x64"
            }
            sources {
                c {
                    source {
                        lib project: ':', library: 'cubiomes', linkage: 'static'
                    }
                }
            }
            binaries.all {
                if (it instanceof SharedLibraryBinarySpec) {
                    sharedLibraryFile = appendBinarySuffix(sharedLibraryFile, "_${targetPlatform.architecture.getName()}")
                } else if (it instanceof StaticLibraryBinarySpec) {
                    staticLibraryFile = appendBinarySuffix(staticLibraryFile, "_${targetPlatform.architecture.getName()}")
                }
                def jvmHome = Jvm.current().javaHome
                cCompiler.args '-I', "${jvmHome}/include"
                if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${jvmHome}/include/linux"
                    cCompiler.args '-fPIC'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args '-I', "${jvmHome}/include/win32"
                }
                cCompiler.args '-I', "${projectDir}/src/cubiomes/c"
                cCompiler.args '-O3'
            }
        }
    }
}

tasks.register('buildNativeLib', Copy) {
    group = "build"

    dependsOn(buildDependentsCubij)

    from layout.buildDirectory.dir('libs/cubij/shared').get()
    include 'libcubij_x86-64.so', 'libcubij_aarch64.so', 'cubij_x86-64.dll'
    from layout.buildDirectory.dir('libs/cubij/shared/linux_x64').get()
    include 'libcubij_x86-64.so'
    from layout.buildDirectory.dir('libs/cubij/shared/linux_aarch64').get()
    include 'libcubij_aarch64.so'
    from layout.buildDirectory.dir('libs/cubij/shared/windows_x64').get()
    include 'cubij_x86-64.dll'
    into layout.projectDirectory.dir('src/main/resources')
}

tasks.register('genCHeaders', Exec) {
    group = "build"
    commandLine 'javac', '-h', 'src/cubij/c',
            'src/main/java/de/rasmusantons/cubiomes/BiomeID.java',
            'src/main/java/de/rasmusantons/cubiomes/Cubiomes.java',
            'src/main/java/de/rasmusantons/cubiomes/Dimension.java',
            'src/main/java/de/rasmusantons/cubiomes/MCVersion.java',
            'src/main/java/de/rasmusantons/cubiomes/NativeLibLoader.java',
            'src/main/java/de/rasmusantons/cubiomes/Range.java',
            'src/main/java/de/rasmusantons/cubiomes/Pos.java',
            'src/main/java/de/rasmusantons/cubiomes/Pos3.java',
            'src/main/java/de/rasmusantons/cubiomes/StructureType.java',
            'src/main/java/de/rasmusantons/cubiomes/BiomeFilter.java'
}

clean.doFirst {
    delete layout.projectDirectory.file('src/main/resources/libcubij_x86-64.so')
    delete layout.projectDirectory.file('src/main/resources/libcubij_aarch64.so')
    delete layout.projectDirectory.file('src/main/resources/cubij_x86-64.dll')
}

processResources.dependsOn 'buildNativeLib'
classes.dependsOn 'buildNativeLib'

test {
    useJUnitPlatform()
}
